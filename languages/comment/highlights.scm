; TODO-style: work still to do
((tag
  (prefix)? @constant.comment.todo.prefix
  (name) @constant.comment.todo
  ("(" @constant.comment.todo.bracket
    (user) @constant.comment.todo.user
    ")" @constant.comment.todo.bracket)?
  (
    (prefix)? @constant.comment.todo.prefix
    (text)? @constant.comment.todo.text)*
  )
  (#match? @constant.comment.todo "^(TODO|TODOS|TO-DO)$"))

; Info-style: notes and informational asides
((tag
  (prefix)? @string.comment.info.prefix
  (name) @string.comment.info
  ("(" @string.comment.info.bracket
    (user) @string.comment.info.user
    ")" @string.comment.info.bracket)?
  (
    (prefix)? @string.comment.info.prefix
    (text)? @string.comment.info.text)*
  )
  (#match? @string.comment.info "^(NOTE|INFO|XXX)$"))

; Error-style: bugs and things that need fixing
((tag
  (prefix)? @property.comment.error.prefix
  (name) @property.comment.error
  ("(" @property.comment.error.bracket
    (user) @property.comment.error.user
    ")" @property.comment.error.bracket)?
  (
    (prefix)? @property.comment.error.prefix
    (text)? @property.comment.error.text)*
  )
  (#match? @property.comment.error "^(FIXME|FIX|BUG|ERROR)$"))

; Warn-style: risky code, workarounds, and things to reconsider
((tag
  (prefix)? @keyword.comment.warn.prefix
  (name) @keyword.comment.warn
  ("(" @keyword.comment.warn.bracket
    (user) @keyword.comment.warn.user
    ")" @keyword.comment.warn.bracket)?
  (
    (prefix)? @keyword.comment.warn.prefix
    (text)? @keyword.comment.warn.text)*
  )
  (#match? @keyword.comment.warn "^(HACK|WARNING|WARN|OPTIMIZE|REVIEW|DEPRECATED|IMPORTANT)$"))
